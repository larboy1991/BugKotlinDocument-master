buildscript {
	ext.kotlin_version = '1.3.30'
	repositories {
		mavenCentral()
		maven { url "https://jitpack.io" }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}
plugins {
	id "org.jetbrains.intellij" version '0.4.8'
}

def bugKtDoc = []
def local_path = ""

switch (alternative_ide) {
	case 'IJ':
		break
	case 'CL':
		local_path = clion_path
		bugKtDoc += clion_kn
		break
	case 'AC':
		local_path = appcode_path
		bugKtDoc += appcode_kn
		break
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'kotlin'
	apply plugin: 'org.jetbrains.intellij'
}

intellij {
	type idea_type
	updateSinceUntilBuild false
	version idea_version
	if (local_path != "") {
		localPath local_path
		alternativeIdePath local_path
		bugKtDoc += 'org.intellij.plugins.markdown:181.2668'
	} else {
		bugKtDoc += 'kotlin'
	}
	plugins bugKtDoc.toArray()
}

patchPluginXml {
	pluginDescription(file("description.html").text)
	changeNotes(file("changeNotes.html").text)
}

group groupId
version plugin_version

repositories {
	mavenCentral()
}

dependencies {
	compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	testCompile group: 'junit', name: 'junit', version: '4.12'
}
sourceSets {
	main.java {
		srcDirs += ['src/main/java', 'src/main/kotlin']
	}
}

compileKotlin {
	kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
	kotlinOptions.jvmTarget = '1.8'
}